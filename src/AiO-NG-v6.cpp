// Firmware_Teensy_AiO-NG-v6 is copyright 2025 by the AOG Group
// Firmware_Teensy_AiO-NG-v6 is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
// Firmware_Teensy_AiO-NG-v6 is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
// You should have received a copy of the GNU General Public License along with Foobar. If not, see <https://www.gnu.org/licenses/>.
// Like most Arduino code, portions of this are based on other open source Arduino code with a compatiable license.

#include "Arduino.h"
#include "common.h"
#include "debug.h"
#include "udpHandlers.h"
#include "gnssHandlers.h"
#include "mgHandlers.h"
#include "setup.h"
#include "mongooseStart.h"
#include "KeyaCANBUS.h"
#include "Autosteer.h"
#include "AutosteerPID.h"
#include "serialComm.h"


elapsedMillis configTimer = 0;
uint8_t runCount = 0;

void setup()
{
  delay(3000); // Delay for tesing to allow opening serial terminal to see output
  Serial.begin(115200);
  Serial.print("\r\n\n\n*********************\r\nStarting setup...\r\n");
  Serial.print("Firmware version: ");
  Serial.print(inoVersion);

  setCpuFrequency(600 * 1000000); // Set CPU speed, default is 600mhz, setup.ino

  // ** IP loading & Mongoose/Eth init needs to be first **
  ipSetup();  // Load the IP address from EEPROM and setup the gateway & broadcast addresses
  load_gps(); // Load the GPS settings from EEPROM
  // load_config();                  // Sync the firmware EEPROM values to the GUI
  // set_settings();
  ethernet_init(); // Bring up the ethernet hardware
  mongoose_init(); // Bring up the mongoose services
  udpSetup();      // Bring up the UDP connections to/from AgIO
  LEDs.init();
  LEDs.set(LED_ID::PWR_ETH, PWR_ETH_STATE::PWR_ON);

  serialSetup();        // Configure the Serial comms
  parserSetup();        // Load the NMEA parser callbacks
  umparserSetup();      // Load Unicore parser callbacks
  BNO.begin(SerialIMU); // Start the IMU
  autosteerSetup();     // Initialize autosteer
  CAN_Setup();          // Start CAN3 for Keya

  outputsInit();            // Initialize PCA9685 for LOCK, AUX & Sections/Machine outputs, enable AUX output but leave others Hi-Z
  machinePTR = new MACHINE; // need to use pointer otherwise Mongoose has a seizure
  machinePTR->init(500);    // 500 is starting address for machine EEPROM storage
  // machinePTR.setSectionOutputsHandler(updateSectionOutputs);
  machinePTR->setMachineOutputsHandler(updateMachineOutputs);
  machinePTR->setUdpReplyHandler(machinePgnReplies);
  initMachineOutputs();

  load_kwas(); // Load IKWASdata from EEPROM

  mongoose_set_http_handlers("reboot", teensyCheckReboot, teensyStartReboot);
  mongoose_set_http_handlers("settings", fw_get_settings, fw_set_settings);
  mongoose_set_http_handlers("ins_cfg", fw_get_ins_cfg, fw_set_ins_cfg);
  mongoose_set_http_handlers("kwas_cfg", fw_get_kwas_cfg, fw_set_kwas_cfg);

  Serial.println("\r\n\nEnd of setup, waiting for GPS...\r\n");
  delay(1);
  resetStartingTimersBuffers(); // setup.ino
}

void loop()
{
  // if (configTimer >= 2000 && g_mgr.ifp->state != MG_TCPIP_STATE_READY && runCount < 3)
  // {
  //   configTimer = 0;
  //   runCount++;
  //   //SerialGPS1.write("CONFIG\r\n");
  //   //SerialGPS1.write("UNILOGLIST\r\n");
  //   SerialGPS1.write("MODE\r\n");
  // }

  GUIusage.timeIn(); // *usage objects are used to track cpu usage on certain sections of code, see debug.h or misc.h
  mongoose_poll();   // update all Mongoose processes, UDP/PGN/Web UI
  GUIusage.timeOut();

  gpsPoll();         // check for data on GPS1 & GPS2 UARTs
  serialESP32();     // check for PGN replies on ESP32 UART
  readKeyaEncoder(); // Read encoder count & speed and current
  KeyaBus_Receive(); // check for Keya data on can bus 3
  autoSteerUpdate(); // run autosteer loop
  serialRTCM();      // check for RTCM data on Xbee/Radio UART

  LEDSusage.timeIn();
  LEDs.updateLoop(); // update frontplate RGB LEDs
  LEDSusage.timeOut();

  MACHusage.timeIn();
  machinePTR->watchdogCheck(); // update machine safety timeout
  MACHusage.timeOut();

  BNOusage.timeIn();
  if (BNO.read())                       // read IMU UART and check for completed RVC updated
  {                                     // there should be new data every 10ms (100hz)
    bnoRing.pushOverwrite(BNO.rvcData); // added IMU update to ring buffer
    bnoStats.incHzCount();              // *Stats objects are used to diag missing/skipped updates for GPS & IMU, see debug.h or misc.h
    bnoStats.update(1);                 // 1 dummy value, normally used to track UART hardware buffer usage
  }
  BNOusage.timeOut();

  checkUSBSerial(); // Check for & process debug cmds

  if (bufferStatsTimer > 5000)
    printTelem(); // Print telemetry

  LOOPusage.timeIn();
  testCounter++; // to count loop hz & get baseline cpu "idle" time
  LOOPusage.timeOut();

  if (SerialRS232.available())
    Serial.write(SerialRS232.read()); // just print to USB for testing
}
